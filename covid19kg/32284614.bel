##################################################################################
# Document Properties
##################################################################################
SET DOCUMENT Name = "Thevarajan_et.al_2020.bel"
SET DOCUMENT Authors = "Priya S"
SET DOCUMENT ContactInfo = "priya@causalitybiomodels.com"
SET DOCUMENT Description = "COVID Knowledge Curation project"

SET DOCUMENT Licenses = "Private Ownership to Fraunhofer Institute SCAI"
SET DOCUMENT Copyright = "Copyright © 2020 Fraunhofer Institute SCAI, All rights reserved"
SET DOCUMENT Version = "2.0.0"


##################################################################################
# NAMESPACES Section
##################################################################################
DEFINE NAMESPACE HGNC AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hgnc/hgnc-20190708.belns"
DEFINE NAMESPACE MESH AS URL 	"https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh/mesh-names-20181007.belns"
DEFINE NAMESPACE TAX AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/ncbi-taxonomy/ncbi-taxonomy-20200322.belns"
#not given in template.bel
DEFINE NAMESPACE DO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20200407.belns"
DEFINE NAMESPACE UNIPROT AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE dbSNP AS PATTERN "rs[0-9]+"

##################################################################################
# ANNOTATIONS Section
##################################################################################
DEFINE ANNOTATION Species AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/species-taxonomy-id/species-taxonomy-id-20170511.belanno"
DEFINE ANNOTATION Sample AS LIST {"Blood","nasopharyngeal swab","sputum","feces","Plasma"}
DEFINE ANNOTATION Assay AS LIST {"real-time reverse-transcriptase polymerase chain reaction"}
DEFINE ANNOTATION Timeline AS LIST {"4 days","5-6 days","7-9 days","20 days"}

# Document Annotations #
DEFINE ANNOTATION PublicationType AS LIST {"Case report"}
DEFINE ANNOTATION PublicationStatus AS LIST {"Published"}
DEFINE ANNOTATION Section AS LIST {"Full Text"}

##################################################################################
# Statements #
##################################################################################

SET Citation = {"PubMed"," 32284614"}

SET PublicationType = "Case report"
SET PublicationStatus = "Published"
SET Section = "Full Text"

SET Species = "9606"
SET Evidence = "Increased antibody-secreting cells (ASCs),follicular helper T cells (TFH cells), activated CD4+ T cells and CD8+ T cells and immunoglobulin M (IgM) and IgG antibodies that bound the COVID-19- causing coronavirus SARS-CoV-2 were detected in blood before symptomatic recovery."

a(TAX:"Severe acute respiratory syndrome coronavirus 2") -> path(DO:"COVID-19")
SET Sample = "Blood"
path(DO:"COVID-19") -> a(MESH:"Antibody-Producing Cells")
path(DO:"COVID-19") -> a(MESH:"T-Lymphocytes, Helper-Inducer")
path(DO:"COVID-19") -> a(MESH:"CD8-Positive T-Lymphocytes")
path(DO:"COVID-19") -> a(MESH:"Immunoglobulin M")
path(DO:"COVID-19") -> a(MESH:"Immunoglobulin G")
UNSET Sample

SET Evidence = "At presentation on day 4, SARS-CoV-2 was detected in a nasopharyngeal swab specimen by real-time reverse-transcriptase PCR. SARS-CoV-2 was again detected at days 5–6 in nasopharyngeal, sputum and fecal samples, but was undetectable from day 7 (Fig.1a)."

SET Assay = "real-time reverse-transcriptase polymerase chain reaction"
SET Sample = "nasopharyngeal swab"
SET Timeline = "4 days"
a(TAX:"Severe acute respiratory syndrome coronavirus 2") -> path(DO:"COVID-19")
UNSET Timeline
UNSET Sample
SET Timeline = "5-6 days"
SET Sample = {"nasopharyngeal swab","sputum","feces"}
a(TAX:"Severe acute respiratory syndrome coronavirus 2") -> path(DO:"COVID-19")
UNSET Sample
UNSET Timeline
UNSET Assay

SET Evidence = "Blood C-reactive protein was elevated at 83.2, with normal counts of lymphocytes (4.3 × 109 cells per liter (range, 4.0 × 109 to 12.0 × 109 cells per liter)) and neutrophils (6.3 × 109 cells per liter (range, 2.0 × 109 to 8.0 × 109 × 109 cells per liter))."

SET Sample = "Blood"
path(DO:"COVID-19") -> p(HGNC:CRP)
UNSET Sample

SET Evidence = "As per reports for Ebola and influenza1,4, co-expression of CD38 and HLA-DR on CD8+ T cells (assessed as the frequency of CD38+HLA-DR+ CD8+ T cells) rapidly increased in this patient from day 7 (3.57%) to day 8 (5.32%) and day 9 (11.8%), then decreased at day 20 (7.05%) (Fig. 1e)."
SET Timeline = "7-9 days"
a(MESH:"CD8-Positive T-Lymphocytes") -> a(MESH:"Histocompatibility Antigens Class II")
a(MESH:"CD8-Positive T-Lymphocytes") -> p(HGNC:CD38)
UNSET Timeline
SET Timeline = "20 days"
a(MESH:"CD8-Positive T-Lymphocytes") -| a(MESH:"Histocompatibility Antigens Class II")
a(MESH:"CD8-Positive T-Lymphocytes") -| p(HGNC:CD38)
UNSET Timeline

SET Evidence = "CD38+HLA-DR+ T cells, especially CD8+T cells, produced larger amounts of granzymes A and B and perforin (~34–54% higher) than did their parent cells (CD8+ or CD4+ populations; Fig. 1e)."

a(MESH:"CD8-Positive T-Lymphocytes") -> p(UNIPROT:P12544)
a(MESH:"CD8-Positive T-Lymphocytes") -> p(UNIPROT:P10144)
a(MESH:"CD8-Positive T-Lymphocytes") -> p(UNIPROT:P14222)

SET Evidence = "We found low levels of the chemokine MCP-1 (CCL2) in the patient’s plasma (Extended Data Fig. 2a), although this was comparable to results obtained for healthy donors (22.15 ± 13.81; n = 5), patients infected with influenza A virus or influenza B, assessed at days 7–9 (33.85 ± 30.12; n = 5), and a patient infected with the human coronavirus HCoV-229e (40.56)."
SET Sample = "Plasma"
path(DO:"COVID-19") -> p(HGNC:CCL2)
UNSET Sample

SET Evidence = "As the single-nucleotide polymorphism rs12252-C/C in the gene IFITM3 (which encodes interferon-induced transmembrane protein 3) is linked to severe influenza6,7, we analyzed IFITM3-rs12252 in the patient with COVID-19 and found the ‘risk’ IFITM3-rs12252-C/C variant (Extended Data Fig. 2b)."
g(dbSNP:rs12252) -- p(HGNC:IFITM3)
p(HGNC:IFITM3) -- path(DO:influenza)
g(dbSNP:rs12252) pos path(DO:"COVID-19")

UNSET Species
UNSET PublicationType
UNSET PublicationStatus
UNSET Section
