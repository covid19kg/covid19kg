##################################################################################
# Document Properties
##################################################################################
SET DOCUMENT Name = "Diao_et.al_2020.bel"
SET DOCUMENT Authors = "Abish K"
SET DOCUMENT ContactInfo = "abish@causalitybiomodels.com"
SET DOCUMENT Description = "COVID Knowledge Curation project"

SET DOCUMENT Licenses = "Private Ownership to Fraunhofer Institute SCAI"
SET DOCUMENT Copyright = "Copyright © 2020 Fraunhofer Institute SCAI, All rights reserved"
SET DOCUMENT Version = "2.0.0"

##################################################################################
# NAMESPACES Section
##################################################################################
DEFINE NAMESPACE HGNC AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hgnc/hgnc-20190708.belns"
DEFINE NAMESPACE MESH AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh/mesh-names-20181007.belns"
DEFINE NAMESPACE DO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20200407.belns"

##################################################################################
# ANNOTATIONS Section
##################################################################################
DEFINE ANNOTATION Species AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/species-taxonomy-id/species-taxonomy-id-20170511.belanno"
DEFINE ANNOTATION MeSHAnatomy AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20190128.belanno"
DEFINE ANNOTATION Custom_Disease AS LIST {"COVID-19"}
DEFINE ANNOTATION Technique AS LIST {"fluorescence-activated cell sorting"}
DEFINE ANNOTATION Cohort AS LIST {"ICU patients"}
DEFINE ANNOTATION Disease_Stage AS LIST {"declining stage","prodromal stage","Symptomatic"}

# Document Annotations #
DEFINE ANNOTATION PublicationType AS LIST {"Research"}
DEFINE ANNOTATION PublicationStatus AS LIST {"Preprint"}
DEFINE ANNOTATION Section AS LIST {"Results"}

##################################################################################
# Statements #
##################################################################################
SET Citation = {"PubMed","32425950"}
SET PublicationType = "Research"
SET Section = "Results"

SET Evidence = "5.75% (359/499), 75.95% (379/499) and 71.54% (357/499) patients had remarkably low total T cell counts, CD4 + and CD8 + T cell counts, respectively."

SET Species = "9606"
SET Custom_Disease = "COVID-19"
path(DO:"COVID-19") -| a(MESH:"T-Lymphocytes")
path(DO:"COVID-19") -| a(MESH:"CD4-Positive T-Lymphocytes")
path(DO:"COVID-19") -| a(MESH:"CD8-Positive T-Lymphocytes")

SET Evidence = "The counts of total T cells, CD4 + and CD8 + T cells were significantly lower in ICU patients than Non-ICU cases (Figure 1B)."

SET Cohort = "ICU patients"
path(DO:"COVID-19") -| a(MESH:"T-Lymphocytes")
path(DO:"COVID-19") -| a(MESH:"CD4-Positive T-Lymphocytes")
path(DO:"COVID-19") -| a(MESH:"CD8-Positive T-Lymphocytes")
UNSET Cohort

SET Evidence = "We only found the levels of TNF-α, IL-6 and IL-10 were significantly increased in infected patients, and statistical analysis illustrated that their levels in ICU patients are significantly higher than in Non-ICU patients (Figure 2A)."

SET Cohort = "ICU patients"
path(DO:"COVID-19") pos p(HGNC:TNF)
path(DO:"COVID-19") pos p(HGNC:IL6)
path(DO:"COVID-19") pos p(HGNC:IL10)
UNSET Cohort

SET Evidence = "Interestingly, the concentration of these three cytokines was negatively correlated with total T cell counts, CD4 + counts, and CD8 + counts, respectively (Figure 2B)."

SET Cohort = "ICU patients"
p(HGNC:TNF) neg a(MESH:"T-Lymphocytes")
p(HGNC:IL6) neg a(MESH:"CD4-Positive T-Lymphocytes")
p(HGNC:IL10) neg a(MESH:"CD8-Positive T-Lymphocytes")
UNSET Cohort

SET Evidence = "Interestingly, serum levels of IFN-γ, IL-10, IL-6 and TNF-α were significantly decreased in these patients in the decline period as compared with illness period, while counts of total T cells, CD4 + , and CD8 + T cell subsets recovered during the decline period (Figure 2C)."

SET Disease_Stage = "declining stage"
SET MeSHAnatomy = "Serum"
path(DO:"COVID-19") neg p(HGNC:IFNG)
path(DO:"COVID-19") neg p(HGNC:IL10)
path(DO:"COVID-19") neg p(HGNC:IL6)
path(DO:"COVID-19") neg p(HGNC:TNF)
path(DO:"COVID-19") pos a(MESH:"T-Lymphocytes")
path(DO:"COVID-19") pos a(MESH:"CD4-Positive T-Lymphocytes")
path(DO:"COVID-19") pos a(MESH:"CD8-Positive T-Lymphocytes")
UNSET MeSHAnatomy
UNSET Disease_Stage

SET Evidence = "FACs analysis illustrated that, T cells from COVID-19 patients have markedly higher levels of PD-1 compared to healthy controls (Figure 3A)."

SET Technique = "fluorescence-activated cell sorting"
path(DO:"COVID-19") pos surf(p(HGNC:PDCD1))
surf(p(HGNC:PDCD1)) -- a(MESH:"T-Lymphocytes")
UNSET Technique

SET Evidence = "Furthermore, statistical analysis showed that the percentage of PD-1 + CD8 + T cells from ICU patients was significantly higher than from both Non-ICU cases and healthy controls (Figure 3B), indicating that SARS-CoV-2 viruses induce T cell exhaustion in COVID-19 patients, particularly in those requiring ICU care."

SET Cohort = "ICU patients"
path(DO:"COVID-19") -> a(MESH:"CD8-Positive T-Lymphocytes")
path(DO:"COVID-19") pos surf(p(HGNC:PDCD1))
surf(p(HGNC:PDCD1)) -- a(MESH:"CD8-Positive T-Lymphocytes")
UNSET Cohort

SET Evidence = "FACs showed that these patients have very low level of PD-1 and Tim-3 on CD8 + and CD4 + T cells in the prodromal stage, however, their levels on CD8 + T cells was increased in overtly symptomatic stages, and highest levels were seen in ICU period (Figure 3C, D)."

SET Technique = "fluorescence-activated cell sorting"
SET Disease_Stage = "prodromal stage"
path(DO:"COVID-19") neg surf(p(HGNC:PDCD1))
surf(p(HGNC:PDCD1)) -- a(MESH:"CD8-Positive T-Lymphocytes")
path(DO:"COVID-19") neg surf(p(HGNC:HAVCR2))
surf(p(HGNC:HAVCR2)) -- a(MESH:"CD4-Positive T-Lymphocytes")
UNSET Disease_Stage
SET Cohort = "ICU patients"
SET Disease_Stage = "Symptomatic"
path(DO:"COVID-19") pos surf(p(HGNC:PDCD1))
surf(p(HGNC:PDCD1)) -- a(MESH:"CD8-Positive T-Lymphocytes")
UNSET Disease_Stage
UNSET Cohort
UNSET Technique

SET Evidence = "Similarly, higher levels of Tim-3 was observed on CD4 + T cells from patients who are in ICU stage, although enhancing the expression of PD-1 on CD4 + T cells was not obviously during disease progress (Figure 3C, D)."

SET Cohort = "ICU patients"
SET Disease_Stage = "Symptomatic"
path(DO:"COVID-19") pos surf(p(HGNC:HAVCR2))
surf(p(HGNC:HAVCR2)) -- a(MESH:"CD4-Positive T-Lymphocytes")
UNSET Disease_Stage
UNSET Cohort
UNSET Species
UNSET Section
UNSET PublicationType
UNSET Citation
